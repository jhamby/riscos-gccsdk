# Top-level configure.in for the RISC OS GCCSDK
# Copyright (c) 2000, 2001, 2002, 2003, 2004 Nick Burrett
# Written by Nick Burrett <nick@dsvr.net>

AC_PREREQ(2.50)
AC_INIT(Makefile.in)
AC_CONFIG_HEADER(sdk-config.h)
AC_CANONICAL_SYSTEM
AC_ARG_PROGRAM

dnl Global version variable
PACKAGE=GCCSDK
VERSION="3.3.3"
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE", [Program name])
AC_DEFINE_UNQUOTED(VERSION, "$VERSION", [Program version])

dnl Checks for programs
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB
AC_PROG_AWK

dnl Locate a program and check that its version is acceptable.
dnl AC_PROG_CHECK_VER(var, name, version-switch,
dnl                  version-extract-regexp, version-glob)
AC_DEFUN([gcc_AC_CHECK_PROG_VER],
[AC_CHECK_PROG([$1], [$2], [$2])
if test -n "[$]$1"; then
  # Found it, now check the version.
  AC_CACHE_CHECK(for modern $2, gcc_cv_prog_$2_modern,
[changequote(<<,>>)dnl
  ac_prog_version=`<<$>>$1 $3 2>&1 |
                   sed -n 's/^.*patsubst(<<$4>>,/,\/).*$/\1/p'`
changequote([,])dnl
  echo "configure:__oline__: version of $2 is $ac_prog_version" >&AC_FD_CC
changequote(<<,>>)dnl
  case $ac_prog_version in
    '')     gcc_cv_prog_$2_modern=no;;
    <<$5>>)
            gcc_cv_prog_$2_modern=yes;;
    *)      gcc_cv_prog_$2_modern=no;;
  esac
changequote([,])dnl
])
else
  gcc_cv_prog_$2_modern=no
fi
])


gcc_AC_CHECK_PROG_VER(MAKEINFO, makeinfo, --version,
  [GNU texinfo.* \([0-9][0-9.]*\)],
  [4.[2-9]*])

AC_CHECK_PROG(FLEX, flex, flex)
AC_CHECK_PROG(GPERF, gperf, gperf)
AC_CHECK_PROG(GREP, grep, grep)
AC_CHECK_PROG(BISON, bison, bison)
AC_CHECK_PROG(SED, sed, sed)

dnl Check the GCC version
AC_PROG_CXX
gxx_version=`${CXX} --version`
AC_C_BIGENDIAN

# See if the system preprocessor understands the ANSI C preprocessor
# stringification operator.
AC_MSG_CHECKING(whether cpp understands the stringify operator)
AC_CACHE_VAL(gcc_cv_c_have_stringify,
[AC_TRY_COMPILE(,
[#define S(x)   #x
char *test = S(foo);],
gcc_cv_c_have_stringify=yes, gcc_cv_c_have_stringify=no)])
AC_MSG_RESULT($gcc_cv_c_have_stringify)
if test $gcc_cv_c_have_stringify = yes; then
  AC_DEFINE(HAVE_CPP_STRINGIFY, 1, [Define if the system preprocesses understands the stringification operator])
fi

dnl Checks for header files
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_HEADER_TIME
AC_CHECK_HEADERS(stdint.h inttypes.h endian.h machine/endian.h)
AC_CHECK_HEADERS(vfork.h fcntl.h limits.h stab.h stddef.h stdlib.h)
AC_CHECK_HEADERS(string.h strings.h sys/file.h sys/param.h sys/resource.h)
AC_CHECK_HEADERS(sys/stat.h sys/time.h sys/times.h time.h unistd.h)

AC_CHECK_FUNCS(atoll atoq)
AC_CHECK_FUNCS(bcmp bcopy bsearch bzero getrlimit gettimeofday index)
AC_CHECK_FUNCS(isascii kill popen putenv setrlimit strchr strerror)
AC_CHECK_FUNCS(strrchr strsignal strtoul sysconf stpcpy strndup)
AC_CHECK_FUNCS(putc_unlocked fputc_unlocked fputs_unlocked)

AC_DECL_SYS_SIGLIST
AC_FUNC_VFORK


dnl Checks for typedefs, structures and compiler characteristics
#AC_CHECK_TYPE(uint32_t, unsigned int)
#AC_CHECK_TYPE(int32_t, int)

dnl Additional configure command line parameters
AC_ARG_WITH(riscos-dist,
	[  --with-riscos-dist=DIR    path to RISC OS distribution install dir])
test "$with_riscos_dist" = yes && with_riscos_dist=$prefix/riscos-dist
test "$with_riscos_dist" = default && with_riscos_dist=$prefix/riscos-dist
test "$with_riscos_dist" = "" && with_riscos_dist=$prefix/riscos-dist

AC_ARG_WITH(riscos-pkg,
	[  --with-riscos-pkg=DIR    path to RISC OS distribution package])
test "$with_riscos_pkg" = yes && with_riscos_pkg=$prefix/riscos-pkg
test "$with_riscos_pkg" = default && with_riscos_pkg=$prefix/riscos-pkg
test "$with_riscos_pkg" = "" && with_riscos_pkg=$prefix/riscos-pkg

AC_ARG_ENABLE(filetype-ff8,
	[  --enable-filetype-ff8    add ,ff8 to RISC OS executables])
if test "$enable_filetype_ff8" = yes; then
  AC_DEFINE(ENABLE_FILETYPE_FF8, 1, [Define if the linker should append ,ff8 to all executables in the cross-compiler])
  exesfx=,ff8
else
  exesfx=
fi
AC_SUBST(exesfx)

dnl Checks for library functions
echo Targeting for host $host
# Get CPU info
case "$host" in
  arm*-*)
	cpu_type="ARM"
	;;
changequote(,)dnl
  i[34567]86-*)
changequote([,])dnl
	cpu_type="I386"
	;;
  mips-*)
        cpu_type="MIPS"
        ;;
esac

# Determine operating system
case "$host" in
  *freebsd*)
	os_type="FREEBSD"
	;;
  *openbsd*)
	os_type="OPENBSD"
	;;
  *linux*)
	os_type="LINUX"
	;;
  *irix*)
        os_type="IRIX"
        ;;
  *cygwin*)
	os_type="CYGWIN"
	;;
esac


target_name="arm-riscos-aof"
target_defs="-DTARGET_RISCOSAOF"
host_system="HOST_${cpu_type}_${os_type}"
AC_SUBST(target_name)
AC_SUBST(target_defs)
AC_SUBST(host_system)
AC_SUBST(with_riscos_dist)
AC_SUBST(with_riscos_pkg)

# Get an absolute path to the GCCSDK top-level source directory
holddir=`pwd`
cd $srcdir
topdir=`pwd`
cd $holddir

# Ensure configure is built.
( cd gcc/gcc ; autoheader ; autoconf )

AC_SUBST(topdir)
AC_PROG_MAKE_SET
AC_CONFIG_SUBDIRS([gcc/gcc])
AC_OUTPUT([Makefile])
